sql_stmt_list: ";"* (sql_stmt _SEMI+)+

sql_stmt: select_stmt
        | create_table_stmt
        | create_view_stmt
        | insert_stmt
        | update_stmt
        | delete_stmt

// add WITH
select_stmt.1: k_select k_distinct? subquery_or_expr_list from_clause? \
                where_clause? order_by_clause? limit_clause? offset_clause?

create_table_stmt.1: k_create k_table table_name LPAREN column_def_list RPAREN

create_view_stmt: k_create k_view table_name k_as LPAREN subquery_or_expr_as RPAREN

insert_stmt: k_insert k_into table_name (LPAREN column_list RPAREN)? k_values values_list

update_stmt: k_update table_name k_set assign_list where_clause?

delete_stmt: k_delete k_from table_name where_clause?

column_list: NAME ("," NAME)*

values_list: value_tuple ("," value_tuple)*

value_tuple: "(" expr ("," expr)* ")"

assign_list: column_name EQUAL VALUE ("," column_name EQUAL VALUE)*

// include VALUE to ensure semicolons in strings are handled properly
// use column_name to match any unidentified keyword or column name
unexpected_stmt.0: (ERR | VALUE | LPAREN unexpected_stmt RPAREN) +

ERR.0: /.+?/

subquery_or_expr_list: subquery_or_expr_as ("," subquery_or_expr_as)*

subquery_or_expr_as: expr (k_as NAME)?

// TODO: expr not allowed here
from_clause: k_from subquery_or_expr_as join_clause*

column_def_list: column_def ("," column_def)*

column_def: NAME sql_type?

join_clause: (k_inner | k_left | k_right | k_full k_outer)? k_join \
                subquery_or_expr_as (k_on expr)?

where_clause: k_where expr

order_by_clause: k_order k_by column_name (k_asc | k_desc)

limit_clause: k_limit POS_INT

offset_clause: k_offset POS_INT

// TODO: expand expr. e.g. add EXISTS. Allow queries in expr (e.g. 3494.sql)
// expr_helper resolves shift/reduce conflicts by forcing right associativity
expr: k_not? (expr_helper operator expr | expr_helper)

expr_helper: LPAREN expr RPAREN
           | LPAREN select_stmt RPAREN
           | column_name
           | VALUE
           | k_cast LPAREN expr k_as sql_type RPAREN
           | k_nullif LPAREN expr COMMA expr RPAREN
           | k_coalesce LPAREN subquery_or_expr_list RPAREN
           | k_exists LPAREN select_stmt RPAREN
           | k_case expr? case_list k_end

case_list: k_when expr k_then expr (k_when expr k_then expr)* (k_else expr)?

any_word: table_name "." NAME | NAME

// TODO: fix table names with prefix (e.g main.t0). see 9215.sql
table_name: (NAME ".")? NAME
column_name: NAME ("." NAME)? ("." NAME)?
           | STAR

// TODO: check what names are allowed and how values are specified
name_or_value: NAME
             | VALUE

sql_type: NAME ("("VALUE ")")?

// TODO: add more operators
operator: PLUS | MINUS | STAR | SLASH | EQUAL | GREATER | LESS | k_is | k_or | k_and

// force anonymous literals to appear in tree with ! prefix
!k_select: "SELECT" | "select"
!k_update: "UPDATE" | "update"
!k_create: "CREATE" | "create"
!k_table: "TABLE" | "table"
!k_from: "FROM" | "from"
!k_on: "ON" | "on"
!k_join: "JOIN" | "join"
!k_where: "WHERE" | "where"
!k_limit: "LIMIT" | "limit"
!k_distinct: "DISTINCT" | "distinct"
!k_as: "AS" | "as"
!k_is: "IS" | "is"
!k_or: "OR" | "or"
!k_offset: "OFFSET" | "offset"
!k_cast: "CAST" | "cast"
!k_nullif: NULLIF
!k_insert: "INSERT" | "insert"
!k_into: "INTO" | "into"
!k_values: "VALUES" | "values"
!k_set: "SET" | "set"
!k_delete: "DELETE" | "delete"
!k_not: "NOT" | "not"
!k_and: "AND" | "and"
!k_order: "ORDER" | "order"
!k_by: "BY" | "by"
!k_asc: "ASC" | "asc"
!k_desc: "DESC" | "desc"
!k_view: "VIEW" | "view"
!k_coalesce: "COALESCE" | "coalesce"
!k_exists: "EXISTS" | "exists"
!k_case: "CASE" | "case"
!k_when: "WHEN" | "when"
!k_then: "THEN" | "then"
!k_else: "ELSE" | "else"
!k_end: "END" | "end"
!k_inner: "INNER" | "inner"
!k_left: "LEFT" | "left"
!k_right: "RIGHT" | "right"
!k_full: "FULL" | "full"
!k_outer: "OUTER" | "outer"

PLUS: "+"
MINUS: "-"
STAR: "*"
SLASH: "/"
EQUAL: "="
GREATER: ">"
LESS: "<"

LPAREN: "("
RPAREN: ")"

NULLIF.3: /(NULLIF)|(nullif)/

NAME.2: /[a-zA-Z_]/ /[a-zA-Z0-9_]*/

VALUE.2: /'([^']|'')*'/
       | ("+"|"-")? /[0-9]+/ /(\.[0-9]*)/?
       | "NULL"
       | "null"

POS_INT: /[0-9]+/

_SEMI: ";"
COMMA: ","

%import common.WS
%ignore WS